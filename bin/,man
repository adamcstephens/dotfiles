#!/usr/bin/env nu

def main [package: string, --list: bool, ...args: string] {
  let package = if (($package | find '#') == null) { $"nixpkgs#($package)" } else { $package }
  let package = if ((nix eval $package --apply builtins.attrNames --json | from json | find --regex '^man$' | length) >= 1) {
    $"($package).man"
  } else {
    $package
  }

  let page = if ($args != []) {
    $args
  } else {
    let meta = nix eval  $"($package).meta" --json | complete
    if ($meta.exit_code != 0) {
      print $meta.stderr
      exit $meta.exit_code
    }

    mut bin = $meta.stdout | from json | get mainProgram --ignore-errors
    if ($bin == null) {
      $bin = ($package | split column "#" | get column2)
    }

    $bin
  }

  let path = nix path-info $package | str trim
  $env.MANPATH = $"($path)/share/man"
  print $env.MANPATH

  if ($list) {
    ls $"($env.MANPATH)/man*/*" | select name | each {|m| $m.name | str replace $"($env.MANPATH)/man" "" | str replace --regex "\\.[0-9]\\..*" "" | split column "/" | rename section page} | flatten
  } else {
    nix build --no-link $package
    man $page
  }
}
