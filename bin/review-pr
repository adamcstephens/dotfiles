#!/usr/bin/env nu

def main [--linux: bool = true, --mac: bool = true, pr: string, ...systems: string] {
  let default_systems: list<string> = [] ++ (if $linux {["x86_64-linux", "aarch64-linux"]} else {[]}) ++ (if $mac {["aarch64-darwin", "x86_64-darwin"]} else {[]})
  (if $systems == [] { $default_systems } else { $systems }) | enumerate | par-each { |system|
    let sleep_duration = $system.index * 2
    sleep ($"($sleep_duration)sec" | into duration)
    let tempfile = mktemp | split row "\n" | first

    (nixpkgs-review pr
      --run $"cat report.md >> ($tempfile)"
      --system $system.item
      --build-graph nix
      --build-args="--print-build-logs"
      $pr)

    let output = open $tempfile
    rm $tempfile

    $output
  } | str join "\n-----\n\n"
}
