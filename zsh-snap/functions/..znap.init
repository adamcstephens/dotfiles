#!/bin/zsh
zmodload -Fa zsh/files b:zf_mkdir b:zf_rm
zmodload -Fa zsh/zutil b:zparseopts
autoload -Uz add-zsh-hook

if zstyle -T :znap: auto-compile; then
  () {
    setopt localoptions NO_warncreateglobal
    zmodload -Fa zsh/parameter p:funcstack
  }

  source . () {
    builtin ${(%):-%N} "$@"
    local -i ret=$?
    .znap.compile "$1:a" ${(M@)funcstack[@]:#*/*}
    return ret
  }

  add-zsh-hook preexec ..znap.auto-compile
fi

add-zsh-hook zsh_directory_name ..znap.dirname

export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
[[ -d $XDG_CONFIG_HOME/zsh ]] ||
    zf_mkdir -pm 0700 $XDG_CONFIG_HOME/zsh

export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
[[ -d $XDG_CACHE_HOME/zsh ]] ||
    zf_mkdir -pm 0700 $XDG_CACHE_HOME/zsh
[[ -d $XDG_CACHE_HOME/zsh-snap ]] ||
    zf_mkdir -pm 0700 $XDG_CACHE_HOME/zsh-snap

typeset -gH _comp_dumpfile=${_comp_dumpfile:-$XDG_CACHE_HOME/zsh/compdump}
[[ -f $_comp_dumpfile && ${${:-${ZDOTDIR:-$HOME}/.zshrc}:a} -nt $_comp_dumpfile ]] &&
    zf_rm -f $_comp_dumpfile

zstyle -s :completion: cache-path _ ||
    zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/compcache"
zstyle -s ':completion:*' completer _ ||
    zstyle ':completion:*' completer _expand _complete _ignored

.znap.function bindkey 'zmodload zsh/complist'
typeset -gHa _znap_compdef=()
compdef() {
  _znap_compdef+=( "${(j: :)${(@q+)@}}" )
}
compinit() { : }
add-zsh-hook precmd ..znap.compinit
[[ -v functions[_bash_complete] ]] ||
    .znap.function _bash_complete compgen complete '
      autoload -Uz bashcompinit
      bashcompinit
      bashcompinit() { : }
    '

unfunction ${(%):-%N}
