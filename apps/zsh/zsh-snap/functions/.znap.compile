#!/bin/zsh
# compile zsh scripts and functions
# args: [ ( <dir> | <file> ) ... ]
zmodload -F zsh/files b:zf_rm

local src bin line
local -aU files dirs
if (( $# )); then
  files=( $^@(N-.) ) dirs=( $^@(N-/) )
  files+=( $^dirs/*~~*/.*/*~*.zwc(D-.) )
else
  files=( ${(@vu)functions_source} )
fi

while (( ${#files} )); do
  src=$files[1]
  shift files

  if ! [[ -w $src:h ]]; then
    print -Pru2 -- "$src:h %F{red}not writable%f"
    files=( ${files:#${src:h}/[^/]##} )
    continue
  fi

  bin=$src.zwc

  if [[ $bin -ot $src ]]; then
    zf_rm -f -- $bin ||
        continue
  fi

  if ! [[ -r $src ]]; then
    print -Pru2 -- "$src %F{red}not readable%f"
    continue
  fi

  if ! [[ -e $bin ]]; then
    if zcompile -Uz -- $src; then
      print -Pr -- "$src -> %F{green}$bin:t%f"
    else
      zf_rm -f -- $bin
    fi
  fi
done
